#nop Include this line in every file to set the command character.


/* hide -> stun -> backstab -> dtrick -> feign -> repeat */
/* The cycle only repeats reliably if mana is available for feign. */
/* dtrick takes energy, not mana. */

/* Still need to handle incoming attacks. */

#alias {c} {
  #send {consider %1};
}

/* preyd calls jump */
/* hide, attempt stun */
#alias {jump} {
  target {%1};
  #echo {Jumping:  $target};
  #var {fighting} {1};
  #send {hide};
  #send {stun $target};
};
#alias {j} {
  jump {%1};
};

#alias {rejump} {
  #send {dtrick $target};
  #if {1 == $mana} {
    #send {feign};
  };
};
#alias {rj} {
  rejump;
};

#alias {f} {
  #send {feign};
};

#alias {bs} {
  #send {backstab %1};
};

#alias {F} {
  #if {1 == $mana} {
    #echo {Fight with mana:  off};
    #var {mana} {0};
  };
  #else {
    #echo {Fight with mana:  on};
    #var {mana} {1};
  };
};

#alias {target} {
  #var {target} {%1};
};

/* successful stun -> attempt backstab */
#action {You stun %1!!!} {
  #send {backstab $target};
};

/* failed stun -> attempt dtrick, attempt feign */
#action {You try to stun %1 but fail!} {
  rejump;
};

/* successful backstab -> attempt dtrick, attempt feign */
#action {in its back} {
  rejump;
};
#action {in his back} {
  rejump;
};
#action {in her back} {
  rejump;
};

/* failed backstab -> attempt dtrick, attempt feign */
#action {avoids your backstab} {
  rejump;
};

/* successful feign -> attempt hide, attempt stun */
#action {You feign your own death!!! Heheheheheh.} {
  #send {hide};
  #send {stun $target};
};

/* failed feign -> attempt dtrick, attempt feign */
#action {acting isn't very good} {
  rejump;
};
#action {acting is not very convincing} {
  rejump;
};

#action {You can't hit what isn't here.} {
  #var {fighting} {0};
};
#action {Use Backstab on whom?} {
  #var {fighting} {0};
};
#action {Whom do you want to stun?} {
  #var {fighting} {0};
};
#action {You can't find anyone to target with your dirty trick.} {
  #var {fighting} {0};
};
/* Stop fighting when YOU die. */
#action {You are dead!  Sorry...} {
  #var {fighting} {0};
};


#alias {target_long_name} {
  #var {target_string} {%1};
  #replace {target_string} { } {;};
  #foreach {$target_string} {target_substring} {
    #var {target} {$target_substring};
  };
  #echo {Targeting:  $target};
};
