#nop Include this line in every file to set the command character.

#alias mapstart{} {
  #var {mapping} {1};
  #echo {mapping:  on};
  #map return;
}
#alias mapleave{} {
  #var {mapping} {0};
  #map leave;
  #echo {mapping:  off};
}
#alias {M} {
  #if {$mapping == 1} {
    mapleave;
  };
  #else {
    mapstart;
  };
}
#alias {mapwrite} {
  #map write {config/tdome/maps/$map};
  #echo {map written};
}
#alias {mw} {
  mapwrite;
}
#alias mapnear{} {
  #map flag unicode on;
}
#alias mapfar{} {
  #map flag unicode off;
}

#alias runto{} {
  #map run {%1} {$step_time};
}

/* Don't map imaginary rooms when walking into walls.
 * Dream rooms are still an issue.   shows up in
 * response to movement as well as other actions.
 */
#alias {blocked_action} {
  #if {$mapping == 1} {
    #map undo;
  };
  #if {1 == $wander} {
    turn_right;
    turn_right;
  };
};

/* I kind of want to traverse doors and such while wandering (and map them), but if it turns out to be locked I'll get stuck in a loop. */
#nop #alias {traverse} {
#nop   #map move $direction;
#nop   turn_right;
#nop   turn_right;
#nop   #map exit {$direction} command {open %1 $direction; $direction};
#nop   turn_right;
#nop   turn_right;
#nop   #map exit {$direction} command {open %1 $direction; $direction};
#nop   #send {open %1 $direction; $direction};
#nop };

#action {cannot move that way} {
  blocked_action;
};
#action {cannot go that way} {
  blocked_action;
};
#action {can't go that way} {
  blocked_action;
};
#action {need a boat to go} {
  blocked_action;
};
#action {seems to be closed.} {
  blocked_action;
};
#action {You are blocked} {
  blocked_action;
}
#action {A strange force field prevents you from moving} {
  blocked_action;
};

#action {You are too exhausted.} {
  #if {$mapping == 1} {
    #map undo;
  };
}


/* These messages can occur for non-movement actions, which can cause overcorrection. */

#nop #action {Maybe you should get on your feet first?} {
#nop   #if {1 == $patrol} {
#nop     st;
#nop     #if {$mapping == 1} {
#nop       #map undo;
#nop     };
#nop   };
#nop };
#action {No way!  You're fighting for your life!} {
  #if {1 == $patrol} {
    #if {$mapping == 1} {
      #map undo;
    };
  };
};
#nop #action {"In your dreams, or what?"} {
#nop   #if {1 == $patrol} {
#nop     #if {$mapping == 1} {
#nop       #map undo;
#nop     };
#nop   };
#nop };


/* Fix map upon respawn. */
#action {The Information Center of Tek Angeles [ Inside ]} {
  mapstart;
  #map goto info;
};
/* Fix map upon bail out. */
#action {Master Sin's School [ Inside ]} {
  mapstart;
  #map goto sin;
};
/* Fix map upon return after rent. */
#action {Bag's Inn reception [ Inside ]} {
  mapstart;
  #map goto bag;
}


#alias {sin} {#map run {sin} {$step_time};};
#alias {tot} {#map run {tot} {$step_time};};
#alias {zero} {#map run {zero} {$step_time};};
#alias {pawn} {#map run {pawn} {$step_time};};
#alias {subway} {#map run {ta_subway} {$step_time};};
