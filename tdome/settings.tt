#nop Include this line in every file to set the command character.

/* server */
#var {server} {tdome.nukefire.org};
#var {port} {4000};

/* startup */
#alias {start_daemons} {
  #session tdome $server $port;

  #if {1 != $lock_lock} {
    #var {lock_lock} {1};
    lockd;
  };
  #if {1 != $lock_wander} {
    #var {lock_wander} {1};
    wanderd;
  };
  #if {1 != $lock_patrol} {
    #var {lock_patrol} {1};
    patrold;
  };
}

#alias {load_map} {
  #map read               {config/tdome/maps/$map};

  #map flag vtmap         on;
  #map flag AsciiGraphics off;
  #map flag unicode       on;
};
#event {PROGRAM START} {
  start_daemons;
  load_map;
};
#event {SESSION CONNECTED} {
  #send $user;
  #send $pass[1];
};

/* Avoid broken lines due to fragmented packets.  See FAQ. */
#config {packet patch} {0.3};

/* Split command line, map, and output areas. */
/* 
  split's five fields are numeric heights or widths:
    top
    bottom (default 1)
    left
    right
    input (below "bottom") (default 1)

  If a negative number is specified for right width, tht absolute value of that
  number is used as the width of the "scrolling area" (the main box in the
  middle) and the remainder is devoted to the right box.
*/
/* Put map on the right. */
#split 0 1 0 -90 1;

/* personal settings */
#var {user}             {Mercevi};
#script {pass} {bash -c 'pass show aaron/tdome.net/$user'};
#var {wield}            {splint};
#var {dual}             {splint};
#var {container}        {sack};
#var {water_container}  {barrel};
#var {water_place}      {cooler};
#var {water_thing}      {cooler};
#var {food_place}       {mcdeath};
#var {food_thing}       {hamburger};

#var {sleep_time}       {15};
#var {step_time}        {1};

#var {patrol_dark}      {1};
/* Direction commands pile up during combat.  The "no go" messages can be very
 * delayed.  If the next map movement starts before they have resolved, things
 * become inconsistent.  For now, set this kind of high.  Later, make attack
 * operations acquire the movement lock, attempt to acquire it when attacked,
 * and prioritize that defensive lock. */
#var {patrol_time}      {45};

#var {wander_time}      {5};

#var {lockd_interval}   {1};

#var {map}              {world};
