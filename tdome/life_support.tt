#nop Include this line in every file to set the command character.

#alias {life_support_on} {
  #var {life_support} {1};
  #echo {Life Support:  on};
  #alias {toggle_life_support} {life_support_off};
}
#alias {life_support_off} {
  #var {life_support} {0};
  #echo {Life Support:  off};
  #alias {toggle_life_support} {life_support_on};
}
#alias {toggle_life_support} {
  life_support_on;
}
#alias {L} {
  toggle_life_support;
}
life_support_off;


#alias {real_sleep_action} {
}

#alias {sleep_action} {
  #if {1 == $life_support} {
    #echo {SLEEP CYCLE:  START};
    life_support_off;
    #if {1 == $patrol} {
      patrol_off;
      #delay {$patrol_time} {
        #delay {$patrol_time} {
          #send sleep;
          #delay {$sleep_time} {
            #send wake;
            #send stand;
            #send score;
            patrol_on;
            life_support_on;
            #echo {SLEEP CYCLE:  END};
          };
        };
      };
    };
    #else {
      #send sleep;
      #delay {$sleep_time} {
        #send wake;
        #send stand;
        #send score;
        #send look;
        life_support_on;
        #echo {SLEEP CYCLE:  END};
      };
    };
  };
}

#alias {food_action} {
  #if {1 == $life_support} {
    #send get $food_thing from $container;
    #send eat $food_thing;
    #send put all.$food_thing in $container;
  };
}

#alias {water_action} {
  #if {1 == $life_support} {
    #send drink $water_container;
  };
}


/* Try hard to avoid breaking the map. */
#action {Yawn.... Perhaps it is time for a nap?} {
  sleep_action;
};

#action {You are sleepy.} {
  sleep_action;
};

#action {You feel drowsy. You should get some rest.} {
  sleep_action;
};

#action {You have been awake 24 hours plus. You REALLY need some sleep.} {
  sleep_action;
};

#action {You need some food.} {
  food_action;
};

#action {You are hungry.} {
  food_action;
};

#action {You're getting thirsty.} {
  water_action;
};

#action {You are thirsty.} {
  water_action;
};
