#nop Include this line in every file to set the command character.

/* 
 * #delay implies an execution fork.  Be careful.
 */

#alias {step_toward} {
  #path destroy;
  #map find {%1};
  #path walk;
};

#alias {walk_path} {
  #delay {$patrol_step_duration} {
    #var {walking} {0};
    #path {get} {position} {path_position};
    #path {get} {length} {path_length};
    #if {1 != $fighting} {
      #if {$path_position <= $path_length} {
        #delay {$patrol_step_delay} {
          #var {walking} {1};
          #send {sneak};
          #path {walk};
          walk_path;
        };
      };
      #else {
        #math {patrol_points_i} {$patrol_points_i + 1};
        #var {on_path} {0};
      };
    };
    # else {
      #delay {$patrol_step_delay} {
        walk_path;
      };
    };
  };
};

#alias {patrold} {
  #var {patrol_points_i} {%1};
  #if {$patrol_points_i > &patrol_points[]} {
    #var {patrol_points_i} {1};
  };
  #if {1 == $patrol} {
    #if {1 != $on_path} {
      #path {destroy};
      #map {find} {$patrol_points[$patrol_points_i]};
      #path {get} {position} {path_position};
      #path {get} {length} {path_length};
      #if {$path_position <= $path_length} {
        #var {on_path} {1};
        #delay {0} {
          walk_path;
        };
      };
      #else {
        #math {patrol_points_i} {$patrol_points_i + 1};
      };
    };
    #delay {$patrol_loop_delay} {
      patrold {$patrol_points_i};
    };
  };
  #else {
    #delay {$patrol_loop_delay} {
      patrold {$patrol_points_i};
    };
  };
};

#alias {patrol_on} {
  #var {patrol} {1};
  #echo {Patrol:  on};
  #alias {toggle_patrol} {patrol_off};
};
#alias {patrol_off} {
  #var {patrol} {0};
  #echo {Patrol:  off};
  #alias {toggle_patrol} {patrol_on};
};
#alias {toggle_patrol} {
  patrol_on;
};
#alias {p} {
  toggle_patrol;
};
patrol_off;
